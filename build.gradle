plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.23.3'
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

mainClassName = "ryuuri.Main"

group 'fi.luukuton'
version '0.5-SNAPSHOT'

tasks.withType(JavaExec) {
    jvmArgs = ['-Xss256M']
}

repositories {
    jcenter()
    mavenCentral()
}

javafx {
    version = "15.0.1"
    modules = [ 'javafx.controls' ]
}

dependencies {
    implementation 'org.openjfx:javafx-graphics:15.0.1:win'
    implementation 'org.openjfx:javafx-graphics:15.0.1:linux'
    implementation 'org.openjfx:javafx-swing:15.0.1:win'
    implementation 'org.openjfx:javafx-swing:15.0.1:linux'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

jar {
    manifest {
        attributes 'Main-Class':'ryuuri.Main'
    }
}

test {
    useJUnitPlatform {
        includeTags 'UnitTest'
        excludeTags 'Benchmark'
    }
}

task benchmark(type: Test) {
    systemProperty 'test.type', System.getProperty('test.type')
    jvmArgs '-Xss64M'
    useJUnitPlatform {
        includeTags 'Benchmark'
        excludeTags 'UnitTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['ryuuri/Main*', 'ryuuri/ui/*'])
        }))
    }
}
